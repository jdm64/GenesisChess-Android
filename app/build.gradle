plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {
    namespace 'com.chess.genesis'
    compileSdk = 36

    defaultConfig {
        applicationId "com.chess.genesis"
        minSdkVersion 30
        targetSdkVersion 36
        versionCode 72
        versionName "2.0.0-alpha2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    signingConfigs {
        release {
            def props = new Properties()
            def propFile = rootProject.file("keystore.properties")
            if (propFile.exists()) {
                props.load(new FileInputStream(propFile))
            } else {
                props.setProperty('storeFile', debug.storeFile.toString())
                props.setProperty('storePassword', debug.storePassword)
                props.setProperty('keyAlias', debug.keyAlias)
                props.setProperty('keyPassword', debug.keyPassword)
            }

            storeFile file(props['storeFile'])
            storePassword props['storePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    buildFeatures {
        compose true
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
    packagingOptions {
        doNotStrip "*/libandroidx.graphics.path.so"
        doNotStrip "*/libdatastore_shared_counter.so"
    }
}

dependencies {
    implementation 'androidx.compose.material3:material3:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.navigation:navigation-compose:2.9.5'
    implementation 'androidx.room:room-paging:2.8.3'
    implementation 'androidx.room:room-runtime:2.8.3'
    implementation 'androidx.paging:paging-compose:3.3.6'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation platform('com.google.firebase:firebase-bom:34.4.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'org.msgpack:msgpack-core:0.9.10'
    implementation 'org.zeromq:jeromq:0.6.0'
    implementation 'androidx.compose.material:material-icons-extended:1.7.8'

    annotationProcessor 'androidx.room:room-compiler:2.8.3'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.0'
    testImplementation 'org.mockito:mockito-core:5.20.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.7.0'
}
