plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
}

android {
    namespace 'com.chess.genesis'
    compileSdk 33

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = "GenesisChess-${output.baseName}-v${variant.versionName}.apk"
        }
    }
    defaultConfig {
        applicationId "com.chess.genesis"
        minSdkVersion 27
        targetSdkVersion 33
        versionCode 72
        versionName "2.0.0-alpha2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    signingConfigs {
        release {
            def props = new Properties()
            def propFile = rootProject.file("keystore.properties")
            if (propFile.exists()) {
                props.load(new FileInputStream(propFile))
            } else {
                props.setProperty('storeFile', debug.storeFile.toString())
                props.setProperty('storePassword', debug.storePassword)
                props.setProperty('keyAlias', debug.keyAlias)
                props.setProperty('keyPassword', debug.keyPassword)
            }

            storeFile file(props['storeFile'])
            storePassword props['storePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    buildFeatures {
        compose true
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

dependencies {
    implementation 'androidx.compose.material:material:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-compose:2.5.3'
    implementation 'androidx.room:room-paging:2.5.1'
    implementation 'androidx.room:room-runtime:2.5.1'
    implementation 'androidx.paging:paging-compose:1.0.0-alpha17'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation platform('com.google.firebase:firebase-bom:32.1.0')
    implementation 'com.google.firebase:firebase-analytics:21.3.0'
    implementation 'com.google.firebase:firebase-messaging:23.1.2'
    implementation 'org.msgpack:msgpack-core:0.9.3'
    implementation 'org.zeromq:jeromq:0.5.3'

    annotationProcessor 'androidx.room:room-compiler:2.5.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    testImplementation 'org.mockito:mockito-core:5.3.1'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
}
